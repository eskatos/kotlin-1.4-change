import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    // id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.4.10' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
    plugins.withId('org.jetbrains.kotlin.jvm') {
        dependencies {
            implementation platform('org.jetbrains.kotlin:kotlin-bom')
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        }
        tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "1.8"
                apiVersion = "1.3"
                languageVersion = "1.3"
                freeCompilerArgs += [
                        "-java-parameters",
                        "-progressive",
                        "-XXLanguage:+NewInference",
                        "-XXLanguage:+SamConversionForKotlinFunctions",
                        "-XX:ReferencesToSyntheticJavaProperties",
                        "-Xjsr305=strict"
                ]
            }
        }
    }
}
