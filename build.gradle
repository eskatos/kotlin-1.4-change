import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.samWithReceiver.gradle.SamWithReceiverExtension
import org.jetbrains.kotlin.samWithReceiver.gradle.SamWithReceiverGradleSubplugin

buildscript {
    dependencies {
        // classpath "org.jetbrains.kotlin:kotlin-sam-with-receiver:1.3.72"
        classpath "org.jetbrains.kotlin:kotlin-sam-with-receiver:1.4.10"
    }
}

plugins {
    // id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.4.10' apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
    plugins.withId('org.jetbrains.kotlin.jvm') {
        dependencies {
            implementation platform('org.jetbrains.kotlin:kotlin-bom')
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        }
        plugins.apply(SamWithReceiverGradleSubplugin)
        samWithReceiver {
            annotation("api.HasImplicitReceiver")
        }
        tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "1.8"
                apiVersion = "1.3"
                languageVersion = "1.3"
                freeCompilerArgs += [
                        "-java-parameters",
                        "-XXLanguage:+NewInference",
                        "-XXLanguage:+SamConversionForKotlinFunctions",
                        "-XX:ReferencesToSyntheticJavaProperties",
                        "-Xjsr305=strict"
                ]
            }
        }
    }
}
